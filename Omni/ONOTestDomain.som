ONOTestDomain = OstDomain (

    | target requestedSelectors cntReadA cntReadB cntWriteA cntWriteB readLiteral writtenLiteral |

    requestedSelectors = (
        ^ requestedSelectors
    )

    target: targetObj = (
        target := targetObj
    )


    initialize = (
        super initialize.
    
        requestedSelectors := Set new.

        cntReadA := 0.
        cntReadB := 0.
        cntWriteA := 0.
        cntWriteB := 0.

        readLiteral := Vector new.
    )


    readCountA = (
        ^ cntReadA
    )

    readCountB = (
        ^ cntReadB
    )

    readField: idx of: obj = unenforced (
        obj == target ifTrue: [
            idx = 3 ifTrue: [cntReadA := cntReadA + 1.].
            idx = 4 ifTrue: [cntReadB := cntReadB + 1.].
        ].
    
        ^ obj instVarAt: idx
    )

    readLiteral = (
        ^ readLiteral
    )

    readLiteral: literal = unenforced (
        readLiteral add: literal.
        ^ literal value
    )
    
    requestExecutionOf: aSelector with: args on: receiver lookup: cls = unenforced (
        receiver == target ifTrue: [
            requestedSelectors add: aSelector.
        ].
        ^ super requestExecutionOf: aSelector with: args on: receiver lookup: cls
    )
    

    write: val toField: idx of: obj = unenforced (
        obj == target ifTrue: [
            idx = 3 ifTrue: [cntWriteA := cntWriteA + 1.].
            idx = 4 ifTrue: [cntWriteB := cntWriteB + 1.].
        ].

        obj instVarAt: idx put: val.
        ^ obj
    )

    write: val toLiteral: literal = unenforced (
        "All globals are literals, and we treat all literals equal in the VM"
    
        writtenLiteral := literal.
    
        literal value: val.
        ^ val
    )

    writeCountA = (
        ^ cntWriteA
    )

    writeCountB = (
        ^ cntWriteB
    )

    writtenLiteral = (
        ^ writtenLiteral
    )

)
